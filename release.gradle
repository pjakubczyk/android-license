/*
 *
 * It's all about OSS Sonatype from now to the bottom
 *
 */

apply plugin: 'maven'
apply plugin: 'signing'

// Set up for Sonatype OSS repository
def isReleaseVersion = !version.endsWith('SNAPSHOT')

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar the artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask(':uploadArchives') }

    sign configurations.archives
}

def uploadUsername = project.hasProperty('sonatypeUsername') ? project['sonatypeUsername'] : ''
def uploadPassword = project.hasProperty('sonatypePassword') ? project['sonatypePassword'] : ''

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment ->
            if (isReleaseVersion) {
                signing.signPom(deployment)
            }
        }

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
            authentication(userName: uploadUsername, password: uploadPassword)
        }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: uploadUsername, password: uploadPassword)
        }

        pom.artifactId = artifactId

        pom.project {
            name 'android-license'
            packaging 'jar'
            description 'Android Gradle plugin for license coping'

            scm {
                url 'scm:git@github.com:pjakubczyk/android-license.git'
                connection 'scm:git@github.com:pjakubczyk/android-license.git'
                developerConnection 'scm:git@github.com:pjakubczyk/android-license.git'
            }

            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'pjakubczyk'
                    name 'Przemek Jakubczyk'
                }
            }
        }
    }
}